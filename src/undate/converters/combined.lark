%import common.WS
%ignore WS

start: (edtf__start | hebrew__hebrew_date  | hijri__hijri_date )

// Renaming of the import variables is required, as they receive the namespace of this file.
// See: https://github.com/lark-parser/lark/pull/973#issuecomment-907287565

// relative import from edtf/edtf.lark
// NOTE: this results in a prefix of edtf__edtf__
%import .edtf.edtf.edtf -> edtf__start

// relative import from calendars/hebrew/hebrew.lark
%import .calendars.hebrew.hebrew.hebrew_date -> hebrew__hebrew_date
%import .calendars.hebrew.hebrew.day  -> hebrew__day
%import .calendars.hebrew.hebrew.month -> hebrew__month
%import .calendars.hebrew.hebrew.year -> hebrew__year

// relative import from calendars/hijri/hijri.lark
%import .calendars.hijri.hijri.hijri_date -> hijri__hijri_date
%import .calendars.hijri.hijri.day  -> hijri__day
%import .calendars.hijri.hijri.month -> hijri__month
%import .calendars.hijri.hijri.year -> hijri__year



// override hebrew date to omit year-only, since year without calendar is ambiguous
// NOTE: potentially support year with calendar label
%override hebrew__hebrew_date: hebrew__day hebrew__month hebrew__year | hebrew__month hebrew__year 

// same for hijri date, year alone is ambiguous
%override hijri__hijri_date: hijri__day hijri__month hijri__year | hijri__month hijri__year 
